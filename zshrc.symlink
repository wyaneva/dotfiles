ZSH=${HOME}/dotfiles
TEXMFHOME=${HOME}/.texmf

typeset -U config_files
config_files=(${ZSH}/**/*.zsh)

# Requires package 'zsh-syntax-highlighting'
#source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# Load everything but the completion files
for file in ${${config_files}:#*/completion.zsh}
do
  source ${file}
done

# Change XTERM title & set 256-colours
case ${TERM} in
  xterm*)
    export TERM=xterm-256color
    precmd () {print -Pn "\e]0;%n@%m: %~\a"}
    ;;
esac

autoload -Uz compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source ${file}
done

# Run an ssh agent to save the git passphrase when it is first used
if ! pgrep -u "$USER" ssh-agent > /dev/null; then
    ssh-agent > ~/.ssh-agent-thing
fi
if [[ "$SSH_AGENT_PID" == "" ]]; then
    eval "$(<~/.ssh-agent-thing)" > /dev/null
fi

# Add local dynamic libraries to LD_LIBRARY_PATH
export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:/usr/local/lib"
