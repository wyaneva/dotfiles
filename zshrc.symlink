ZSH=${HOME}/dotfiles
TEXMFHOME=${HOME}/.texmf

typeset -U config_files
config_files=(${ZSH}/**/*.zsh)

# Requires package 'zsh-syntax-highlighting'
source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# Load everything but the completion files
for file in ${${config_files}:#*/completion.zsh}
do
  source ${file}
done

# Change XTERM title & set 256-colours
case ${TERM} in
  xterm*)
    export TERM=xterm-256color
    precmd () {print -Pn "\e]0;%n@%m: %~\a"}
    ;;
esac

autoload -Uz compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source ${file}
done

#start an ssh agent on startup
SSH_ENV="$HOME/.ssh/environment"

function start_agent {
    echo "Initialising new SSH agent..."
    /usr/bin/ssh-agent | sed 's/^echo/#echo/' > "${SSH_ENV}"
    echo succeeded
    chmod 600 "${SSH_ENV}"
    . "${SSH_ENV}" > /dev/null
    /usr/bin/ssh-add;
}

# Source SSH settings, if applicable
if [ -f "${SSH_ENV}" ]; then
    . "${SSH_ENV}" > /dev/null
    #ps ${SSH_AGENT_PID} doesn't work under cywgin
    ps -ef | grep ${SSH_AGENT_PID} | grep ssh-agent$ > /dev/null || {
        start_agent;
    }
else
    start_agent;
fi


